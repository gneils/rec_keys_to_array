c Create an array with all key values for a record type.  Parse the array and update the GUI according to the user click
  1) It grab all keys to all records and creates the array.
  2) It looks for the first row that matches user request
  3) It then set all match criteria to first row values
  4) From there it updates several arrays, one for each key with unique values
  5) If a key array needs updating it is based on all prior keys matching the criteria
  6) key array is cycled through making sure value is unique.

retrieval crwarn
string  *  32  array db_rec_keys   ( 100, 8 ) |row, key1, key2, key3, key4, key5, key6
string  *  32  array rec_key_list  ( 100, 8 ) |value, key1, key2, key3, key4, key5, key6
integer *   2  array row_pointer  ( 8 )   | next row to add to array for each key
string  *  32  array match_value   ( 8 )  | User matching criteria.  Updated when 1st match is encountered
integer *   1  key_refresh_start     | First key to refresh
integer *   1  key_count             | how many keys are there
integer *   2  rt#                   | record type number
string  * 150  txt                   | general output for debuggin
integer *   2  db_row                | database row
integer *   1  previous_keys_match   | if all previous keys match check current key value to see if it is unique
integer *   1  first_match           | First row that matched user defined criteria was encountered.  If so set all match values to current row values
string  *  32  current_value 
integer *   1  testing
integer *   1  start_key             | which key was clicked


testing = 0  | When we have grabbed a case with the record type of interest.
window clear
first_match = 0
set row_pointer * (1)  | starting position for list
start_key = 1          | because SIR does complex things to CASE and CASESLESS DB we pass in a start value and change as needed.
key_refresh_start = start_key + dbtype(0) | note CASEID is first key
rt# = recnum('Z1CHECK') | recnum('A4DETAIL')
db_row = 0 
key_count = nkeys(rt#) + dbtype(0)
write ' --------------    new run    --------------'
write 'key count: ' key_count 
write 'key refresh starts at: ' key_refresh_start
PROCESS CASES | old case is 1001
. autoset
. ifthen ( key_count eq 0) 
.   write 'no keys to check'
.   exit cases
. endif

c test parameters
. PROCESS REC Z1CHECK
c   simulate clicking on the first key, first value
.   match_value(1 + dbtype(0)) = varget( keyname( rt#, 1 + dbtype(0) ) )
.   write 'match this case: ' [varget( keyname( rt#, dbtype(0) ) )]
.   write 'match this 1st key value: ' [match_value( 1 + dbtype(0) ) ]
.   testing = 1
.   EXIT REC
. END REC
c
. if (testing = 0) next CASE | didn't have a record.  Move to a case that does

. process rec RECN
|   gets all keys and puts into array.  One record = one row in array.
.   execute subprocedure MAKE_DB_KEY_ARRAY
. end rec
. if (testing = 1) EXIT CASES | got a test case
. write 'total rows in db: ' db_row

END CASES

execute subprocedure Z1CHECK

execute subprocedure VIEW_ALL_KEYS

ifthen (key_count ge 1)
. execute subprocedure CREATE_MATCHED_KEY_ARRAYS
else
. write 'no keys.  exited'
. exit program
endif

execute subprocedure refresh_keys


subprocedure MAKE_DB_KEY_ARRAY
WRITE '-----------   Make DB Array     -----------'
. db_row = db_row + 1
. for each_key = 1 + dbtype(0), key_count
.   db_rec_keys (db_row, each_key) = varget( keyname( rt#, each_key ) )
. end for
end subprocedure


subprocedure view_all_keys
WRITE '---------   ALL  VALUES in Array   --------'
. txt = ""
. for each_db_row = 1, db_row
. txt = format (each_db_row ) + ': ' 
.   for each_key = 1 + dbtype(0), key_count
.     txt = txt + db_rec_keys (each_db_row, each_key) 
.     if(each_key lt key_count) txt = txt + " | "
.   end for
.   write txt
. end for
end subprocedure

subprocedure CREATE_MATCHED_KEY_ARRAYS
WRITE '--------   Create Match Key Arrays --------'
. first_match = 0 | no matching row yet
. for each_db_row = 1, db_row
|   check each row to see if it matches the criteria.  If yes, grab all key values
.   if ( first_match eq 0 ) execute subprocedure CHECK_ALL_MATCH_CRITERIA

.   for each_key = 1 + dbtype(0), key_count
.     current_value = db_rec_keys (each_db_row, each_key)
.     execute subprocedure previous_keys_match
.     ifthen  ( previous_keys_match = 0 ) 
write 'each_key: ' each_key ' previous key values did not match.' 
.       exit for | all key values before this value must match
.     endif
.     execute subprocedure is_value_unique    | if unique add to list
.   end for
. end for
end subprocedure

subprocedure refresh_keys
| here cycle through keys, start with first that needs to be updated. append list
WRITE // '-----------   show values to update -----------'
WRITE 'These keys: ' [key_refresh_start ] ' - ' key_count
. for each_key = key_refresh_start , key_count 
write 'key ' each_key '.  # of values to update: ' [row_pointer(each_key) - 1 ]
.   for each_row = 1, row_pointer(each_key) - 1
.     write 5t '  row: ' each_row   
            'value: ' rec_key_list (each_row, each_key )
.   end for
. end for
end subprocedure 

subprocedure previous_keys_match
write '-----------   previous_keys_match   ---------------'
. ifthen (each_key eq 1 + dbtype(0) ) | special rule for first keys
.   previous_keys_match = 1
.   exit subprocedure
. endif
. previous_keys_match = 0 
. for check_key = 1 + dbtype(0), each_key - 1  | Check all keys up to but not including current key
| write 'checking key: ' check_key '.  db value: ' db_rec_keys (each_db_row, check_key ) ' and match value: ' match_value (check_key) 
.   ifthen ( db_rec_keys (each_db_row, check_key ) eq match_value (check_key) )
.     previous_keys_match = 1
.   else
.     previous_keys_match = 0 
.     exit subprocedure | can exit immediately when a single match fails
.  endif
. end for
end subprocedure

subprocedure is_value_unique
. for each_element = 1, row_pointer(each_key)
| write 5t 'value check: ' rec_key_list ( each_element, each_key ) ' ?= 'current_value 
.   ifthen ( rec_key_list ( each_element, each_key ) eq current_value ) 
| write 'Is value unique: NO! key: ' each_key ' found value: ' current_value
.     exit subprocedure | value is already in list move on.
.   endif
. end for
write 'adding value to key: ' each_key ' = '  current_value 
. rec_key_list (row_pointer(each_key), each_key ) = current_value
. row_pointer(each_key) = row_pointer(each_key) + 1
end subprocedure 

subprocedure CHECK_ALL_MATCH_CRITERIA
write '------ Looking for First Match   ----------'
. if(first_match eq 1) exit subprocedure | values already set
. ifthen (key_refresh_start eq 1 ) | special rule for first keys- grab first row
.   first_match = 1
. else | if all keys upto match key match set flag
write 'checking for matched row' 
.   for match_key = 1 + dbtype(0), key_refresh_start - 1
write 'key' match_key 'DB:'  db_rec_keys (each_db_row, match_key)  ' value: ' match_value( match_key ) 
.     ifthen(  match_value( match_key ) = db_rec_keys (each_db_row, match_key) )
.       first_match = 1 
.     else
.       first_match = 0
.       exit subprocedure
.     endif
.   end for
. endif
. ifthen (first_match eq 1) | set all of the match values.
write 'settting match criteria: ' 
.   for match_key = 1 + dbtype(0), key_count
write 'key ' match_key ' value ' db_rec_keys (each_db_row, match_key)
.       match_value( match_key ) = db_rec_keys (each_db_row, match_key)
.   end for
. endif
end subprocedure



end retrieval